<html>
<head>
  <title>Audio Equalizer | seen.js</title>
  
  <link href="http://fonts.googleapis.com/css?family=Roboto" rel="stylesheet" type="text/css"/>
  
  <link href="css/theme.css" rel="stylesheet" type="text/css"/>
  
  
  <script src="lib/seen.min.js"></script>
  
  <script src="http://cdnjs.cloudflare.com/ajax/libs/coffee-script/1.7.1/coffee-script.min.js"></script>
  
</head>
<body>
  <div class="page">
    <h1>Audio Equalizer</h1>
    <div></div>

        <div class="nav-bar">
      <div class="nav-button-wrapper" style="text-align : left;">
        
          <a href="demo-multi-views.html"><div class="nav-button">
          <div class="label">prev</div><div class="title">Same Scene, Multiple Angles</div>
          </div></a>
        
      </div>
      <div class="nav-button-wrapper" style="text-align : center;">
          <a href="index.html"><div class="nav-button">
          <div class="label">up</div><div class="title">to index</div>
          </div></a>
      </div>
      <div class="nav-button-wrapper" style="text-align : right;">
        
      </div>
    </div>

    
  <div style="width:100%;text-align:center;color:#888;margin-top:40px;margin-bottom:20px;">Audio : <i>Battar</i> by EOTO (CC Attribution 3.0)</div>
  <audio controls id="seen-audio" src="http://archive.org/download/Eoto2010-09-17BellvueCo.Mishwaka/eoto100917d2_05_Battar.mp3" type="audio/mpeg" style="width:100%;">
    Your browser does not support the audio element.
    <!-- https://archive.org/details/Eoto2010-09-17BellvueCo.Mishwaka -->
  </audio>
  <canvas width="900" height="200" id="seen-canvas"></canvas>


    
    

    
<script type="text/coffeescript" src="lib/audio-interface.coffee"></script>
<script type="text/coffeescript" id="code">
  width     = 900
  height    = 200
  barsWidth = 800
  bands     = 16
  bars      = 8

  # Create model and transform so we can see the frequency bands
  model = seen.Models.default()
    .scale(barsWidth/bands, barsWidth/bands, barsWidth/bands)
    .translate(-barsWidth/2*1.2, -100, -100)
    .rotx(0.2)

  # Create bar colors
  colors = 
    low  : new seen.Material(seen.Colors.hsl(0.3, 0.8, 0.5, 0.6))
    med  : new seen.Material(seen.Colors.hsl(0.1, 0.8, 0.6, 0.6))
    high : new seen.Material(seen.Colors.hsl(0.0, 0.7, 0.6, 0.6))

  # Create the frequency band array of shapes
  freqBands = []
  for f in [0...bands]
    freqBands.push [0...bars].map (b) ->
      shape = seen.Shapes.unitcube().scale(1, 0.35, 1).translate(f*1.2, b*0.42, 0)
      shape.visible = if b/bars < 0.5 then colors.low
      else if b/bars < 0.7 then colors.med
      else colors.high
      shape.fill(null)
      model.add(shape)
      return shape

  # Create scene and add shape to model
  scene = new seen.Scene
    cullBackfaces : false
    model         : model
    camera        : new seen.Camera
      viewport : seen.Viewports.center(width, height)

  # Create render context from canvas. Render at its own speed.
  seen.Context('seen-canvas', scene).animate().start()

  # Update the scene with the frequency information
  updateScene = (array) ->
    for val,i in array
      vali = Math.floor(val * bars / 256)
      for bar, b in freqBands[i]
        bar.fill(if vali >= b then bar.visible else null)

  audio = document.getElementById('seen-audio')
  getContextWhenReady audio, (audioContext) ->
    # Create audio processors
    audioSource   = audioContext.createMediaElementSource(audio)
    audioScript   = audioContext.createScriptProcessor(512)
    audioAnalyser = audioContext.createAnalyser()
    audioAnalyser.smoothingTimeConstant = 0.2
    audioAnalyser.fftSize = 32
    
    # Connect audio to speaker output
    audioSource.connect(audioContext.destination)

    # Pipe audio through analyser
    audioSource.connect(audioAnalyser)
    audioAnalyser.connect(audioScript)
    audioScript.connect(audioContext.destination)

    # Update array with frequency data and update scene
    array = new Uint8Array(audioAnalyser.frequencyBinCount)
    audioScript.addEventListener 'audioprocess', (e) ->
      audioAnalyser.getByteFrequencyData(array)
      updateScene(array)
</script>


    
    <link href="http://yandex.st/highlightjs/8.0/styles/monokai_sublime.min.css" rel="stylesheet" type="text/css"/>
<script src="http://yandex.st/highlightjs/8.0/highlight.min.js"></script>
<pre id="code-view"></pre>
<script type="text/javascript">
  var view = document.getElementById('code-view');
  var code = document.getElementById('code');
  view.innerHTML = code.innerHTML;
  hljs.highlightBlock(view);
</script>
    
  </div>
</body>
</html>
